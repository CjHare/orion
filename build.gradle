plugins {
  id "com.diffplug.gradle.spotless" version "3.6.0"
  id 'com.palantir.git-version' version '0.10.0'
  id "com.github.hierynomus.license" version "0.14.0"
  id "com.github.johnrengelman.shadow" version "2.0.2"
  id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java-library'
apply plugin: 'application'
apply plugin: "jacoco"
apply plugin: "scala"
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

apply from: "gradle/check-licenses.gradle"

mainClassName = 'net.consensys.orion.api.cmd.Orion'
group = 'net.consensys'
version = '0.1.0-' + versionDetails().gitHash[0..4] + '-' + getTimestamp()

// JVM Settings for Log4J and VertX
applicationDefaultJvmArgs = [
  '-Dlog4j.configurationFile=log4j2.xml',
  "-Dvertx.disableFileCPResolving=true",
  "-Dvertx.logger-delegate-factory-class-name=io.vertx.core.logging.Log4j2LogDelegateFactory"
]

def buildAliases = ['dev': [
    'spotlessApply',
    'checkLicenses',
    'assemble',
    'test',
    'acceptanceTest'
  ]]

def expandedTaskList = []
gradle.startParameter.taskNames.each {
  expandedTaskList << (buildAliases[it] ? buildAliases[it] : it)
}
gradle.startParameter.taskNames = expandedTaskList.flatten()

repositories { jcenter() }

// additional source sets (acceptance and performance tests)
sourceSets {
  acceptance {
    java.srcDir file('src/acceptance-test/java')
    resources.srcDir file('src/acceptance-test/resources')
    compileClasspath += main.runtimeClasspath + configurations.testRuntime
    runtimeClasspath += main.runtimeClasspath + configurations.testRuntime
  }
  performance {
    scala.srcDir file('src/performance-test/scala')
    resources.srcDir file('src/performance-test/resources')
    compileClasspath += main.runtimeClasspath
    runtimeClasspath += main.runtimeClasspath
  }
}

task acceptanceTest(type: Test) {
  description = 'Runs the acceptance tests'
  group = 'verification'
  setTestClassesDirs(sourceSets.acceptance.output.getClassesDirs())
  classpath = sourceSets.acceptance.runtimeClasspath
  reports.junitXml.setDestination(file("$buildDir/acceptance-results"))
  reports.html.setDestination(file("$buildDir/reports/acceptance"))
}

static def gatlingURL() {
  def appUrl = System.getProperty('gatlingURL')
  appUrl ?: 'http://localhost:8295'
}

task startOrion {
  dependsOn installDist
  doLast {
    copy {
      from "$projectDir/src/performance-test/resources"
      into "$buildDir/install/orion"
      include '*'
    }
    ext.process1 = new ProcessBuilder()
      .directory(new File("$buildDir/install/orion"))
      .command('bin/orion', 'config-8081.txt')
      .start()
    ext.process2 = new ProcessBuilder()
      .directory(new File("$buildDir/install/orion"))
      .command('bin/orion', 'config-8083.txt')
      .start()
    ext.process3 = new ProcessBuilder()
      .directory(new File("$buildDir/install/orion"))
      .command('bin/orion', 'config-8085.txt')
      .start()
  }
}

task stopOrion {
  doLast {
    if (tasks.startOrion.process1 != null) {
      tasks.startOrion.process1.destroy()
    }
    if (tasks.startOrion.process2 != null) {
      tasks.startOrion.process2.destroy()
    }
    if (tasks.startOrion.process3 != null) {
      tasks.startOrion.process3.destroy()
    }
  }
}

task performanceTest(type: JavaExec) {
  description = 'Runs the performance tests'
  group = 'verification'

  dependsOn startOrion
  finalizedBy stopOrion

  main = 'io.gatling.app.Gatling'
  classpath = sourceSets.performance.runtimeClasspath
  args = [
    '--simulation',
    'net.consensys.orion.load.LoadSimulation',
    '--results-folder',
    file('build/reports/performance').absolutePath,
    '--mute'
  ]
  environment = ['appUrl': gatlingURL()]
  systemProperties = ['gatling.core.directory.binaries': sourceSets.test.output.classesDirs]
}


dependencies {
  // vertx
  compile 'io.vertx:vertx-core:3.5.0'
  compile 'io.vertx:vertx-web:3.5.0'

  // crypto
  compile 'org.bouncycastle:bcprov-jdk15on:1.58'
  compile 'com.muquit.libsodiumjna:libsodium-jna:1.0.4'

  // http client
  compile 'com.squareup.okhttp3:okhttp:3.9.1'

  // storage
  compile 'org.fusesource.leveldbjni:leveldbjni-all:1.8'
  compile 'org.mapdb:mapdb:3.0.5'

  // serialization
  compile 'com.moandjiezana.toml:toml4j:0.7.2'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.9.0'
  compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.9.0'
  compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.0'

  // logging
  compile 'org.apache.logging.log4j:log4j-api:2.10.0'
  compile 'org.apache.logging.log4j:log4j-core:2.10.0'
  compile 'org.apache.logging.log4j:log4j-1.2-api:2.10.0'

  // testing
  testCompile 'junit:junit:4.12'

  // mock http server (unit test purposes)
  testCompile 'com.squareup.okhttp3:mockwebserver:3.9.1'

  // performance testing
  performanceCompile 'org.scala-lang:scala-library:2.12.4'
  performanceCompile 'org.scalatest:scalatest_2.12:3.0.4'
  performanceCompile 'io.gatling:gatling-core:2.3.1'
  performanceCompile 'io.gatling:gatling-http:2.3.1'
  performanceCompile 'io.gatling:gatling-app:2.3.1'
  performanceCompile 'io.gatling.highcharts:gatling-charts-highcharts:2.3.1'
}


jar {
  doFirst { versionTxt() }
  manifest {
    attributes('Implementation-Title': project.name,
    'Implementation-Version': version,
    'Main-Class': 'net.consensys.orion.api.cmd.Orion'
    )
  }
}


run {
  description = 'Run Orion. Use -Pargs="" to pass arguments'
  standardInput = System.in

  if (project.hasProperty("args")) {
    args project.getProperty('args').split()
  }
}

spotless {
  java {
    removeUnusedImports()
    eclipse().configFile(rootProject.file('gradle/eclipse-java-consensys-style.xml'))
    importOrder 'net.consensys', 'java', ''
    endWithNewline()
  }
  groovyGradle {
    target '*.gradle'
    greclipse().configFile(rootProject.file('gradle/greclipse-gradle-consensys-style.properties'))
    endWithNewline()
  }
}

task deleteTarGzip() {
  doFirst {
    delete fileTree(dir:'build/distributions', include: '*.tar.gz')
  }
}

distTar {
  compression = Compression.GZIP
  extension = 'tar.gz'
}
distTar.dependsOn deleteTarGzip

distZip {
  doFirst {
    delete fileTree(dir:'build/distributions', include: '*.zip')
  }
}

project.ext.fatJarBaseName = "orion-fat-jar"
shadowDistTar {
  baseName = project.fatJarBaseName
  compression = Compression.GZIP
  extension = 'tar.gz'
}
shadowDistTar.dependsOn deleteTarGzip

shadowDistZip { baseName = project.fatJarBaseName }

shadowJar {
  baseName = project.fatJarBaseName
  classifier = null
}

// Generate the Timestamp for the version
def getTimestamp() {
  def date = new Date()
  return date.format('yyyy-MM-dd-HH-mm-ss-SSS')
}

// Create the version.txt file
def versionTxt()  {
  delete "$projectDir/src/main/resources/version.txt"
  delete "$projectDir/build/resources/main/version.txt"

  new File("$projectDir/build/resources/main/version.txt").text = """Application-name: $project.name
Version: $version
"""
  new File("$projectDir/build/version").text = """$version"""
}

distributions {
  main {
    contents {
      from(checkLicenses) { into "license" }
      from("./LICENSE") { into "license" }
    }
  }
}

publishing {
  publications {
    mavenArtifact(MavenPublication) {
      from components.java
      pom.withXml {
        asNode().children().last() + {
          resolveStrategy = Closure.DELEGATE_FIRST
          name 'Orion'
          description 'A java implementation of Constellation'
          url 'https://github.com/ConsenSys/orion'
          scm { url 'https://github.com/ConsenSys/orion' }
          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/license/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }
        }
      }
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')
  publications = ['mavenArtifact']
  pkg {
    repo = "pegasys-repo"
    name = "orion"
    userOrg = "consensys"
    licenses = ["Apache-2.0"]
    vcsUrl = "https://github.com/ConsenSys/orion"
    version { name = project.version }
  }
}
